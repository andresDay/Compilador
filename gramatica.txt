inicio' ---> inicio
inicio ---> DECVAR declaraciones ENDDEC programa
inicio ---> programa
declaraciones ---> lista_de_variables DECLARACION tipodato P_COMA
lista_de_variables ---> ID
lista_de_variables ---> lista_de_variables COMA ID
tipodato ---> STRING
tipodato ---> CHAR
tipodato ---> INTEGER
programa ---> bloque
bloque ---> sentencia
bloque ---> bloque sentencia
sentencia ---> ciclo
sentencia ---> ciclo_especial
sentencia ---> asignacion
sentencia ---> seleccion
sentencia ---> funcion
funcion ---> ESCRIBIR P_A CTE_CADENA P_C P_COMA
funcion ---> ESCANEAR P_A ID P_C P_COMA
funcion ---> CONCATENAR P_A ID COMA CTE_CADENA P_C P_COMA
seleccion ---> IF P_A condicion_mul P_C START bloque END
seleccion ---> IF P_A condicion_mul P_C START bloque END ELSE START bloque END
condicion_mul ---> condicion AND condicion
condicion_mul ---> condicion OR condicion
condicion_mul ---> NOT condicion
condicion_mul ---> condicion
condicion ---> expresion MAYOR expresion
condicion ---> expresion MENOR expresion
condicion ---> expresion MAYOR_IGUAL expresion
condicion ---> expresion MENOR_IGUAL expresion
condicion ---> expresion IGUAL expresion
condicion ---> expresion DISTINTO expresion
asignacion ---> ID ASIG expresion P_COMA
expresion ---> expresion OP_SUMA termino
expresion ---> expresion OP_RESTA termino
expresion ---> termino
termino ---> termino OP_MULT factor
termino ---> termino OP_DIV factor
termino ---> termino OP_MOD factor
termino ---> factor
factor ---> P_A expresion P_C
factor ---> ID
factor ---> CTE_INT
factor ---> CTE_REAL
ciclo ---> WHILE P_A condicion_mul P_C START bloque END
ciclo_especial ---> WHILE ID IN C_A lista_de_expresiones C_C DO bloque ENDWHILE
lista_de_expresiones ---> factor
lista_de_expresiones ---> lista_de_expresiones COMA factor